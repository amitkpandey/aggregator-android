apply plugin: 'com.android.application'

android {
    compileSdkVersion 21
    buildToolsVersion '21.0.2'

    defaultConfig {
        applicationId 'com.tughi.aggregator.dev'
        minSdkVersion 14
        targetSdkVersion 21
        versionCode 100
        versionName '3.0-dev'
    }
}

dependencies {
    compile project(':sqlite')
    compile project(':core')
    compile project(':xml')
    compile 'com.android.support:appcompat-v7:21.0.2'
    compile 'com.android.support:recyclerview-v7:21.0.0'
    compile 'com.squareup.picasso:picasso:2.4.0'
}

task generateDrawables << {
    def inkscapeCmd = '/Applications/Inkscape.app/Contents/Resources/bin/inkscape'

    def svgDir = new File(projectDir, ['src', 'main', 'svg'].join(File.separator))
    def resDir = new File(projectDir, ['src', 'main', 'res'].join(File.separator))

    def DENSITIES = [
            'drawable-mdpi'   : 90,
            'drawable-hdpi'   : 135,
            'drawable-xhdpi'  : 180,
            'drawable-xxhdpi' : 270,
            'drawable-xxxhdpi': 360
    ]

    new File(svgDir, 'drawable').eachFileMatch(~/.*\.svg/) { svg ->
        DENSITIES.each { type, dpi ->
            def png = new File(new File(resDir, type), svg.name.replace('.svg', '.png'))
            if (!png.exists() || png.lastModified() < svg.lastModified()) {
                "$inkscapeCmd -e $png -d$dpi $svg"
                        .execute()
                        .waitFor()
            }
        }
    }
}

preBuild.dependsOn generateDrawables